plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version '0.18.2'
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    jwtVersion = '0.12.3'
}

dependencies {

    // spring boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // webclient
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.68.Final:osx-aarch_64'

    // emoji
    implementation 'com.vdurmont:emoji-java:5.1.1'

    // db
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // swagger & restdocs
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2' // For OpenApi Wrapper
    testImplementation 'com.epages:restdocs-api-spec-restassured:0.18.2'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
    testImplementation 'io.rest-assured:rest-assured:5.3.0'
    swaggerUI 'org.webjars:swagger-ui:4.11.1'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:' + jwtVersion
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:' + jwtVersion
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:' + jwtVersion

    // aws
    implementation platform('software.amazon.awssdk:bom:2.15.0')
    implementation 'software.amazon.awssdk:s3control'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
}

processResources.dependsOn('copySecret', 'copyFlyway')

tasks.register('copySecret', Copy) {
    from './submodule'
    include "application*.yml"
    into './src/main/resources/'
}

tasks.register('copyFlyway', Copy) {
    from './submodule/flyway'
    into './src/main/resources/flyway/'
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'dev' : profile

tasks.named('jar') {
    enabled = false
}

swaggerSources {
    sample {
        setInputFile(file("${buildDir}/api-spec/openapi3.yaml"))
    }
}

openapi3 {
    servers = [
            { url = "https://dev.api.ba-ro.co.kr" },
            { url = "https://api.ba-ro.co.kr" },
            { url = "http://localhost:8080" }
    ]
    title = "Baro API"
    description = "Baro API Docs"
    version = "1.0.0"
    format = "yaml"
}

tasks.withType(GenerateSwaggerUI) {
    dependsOn 'openapi3'
}

tasks.register('copySwaggerUI', Copy) {
    dependsOn 'resolveMainClassName'
    dependsOn 'generateSwaggerUISample'

    def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()

    from("${generateSwaggerUISampleTask.outputDir}")
    into "${buildDir}/resources/main/static/docs"
}

tasks.register('copyToLocal', Copy) {
    dependsOn 'copySwaggerUI'
    from("${buildDir}/resources/main/static/docs")
    into './src/main/resources/static/docs'
}

bootJar {
    dependsOn 'copySecret'
    dependsOn 'copyToLocal'
}

